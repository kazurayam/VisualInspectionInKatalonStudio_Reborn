// VisualTestingInKatalonStudio_revive/build.gradle
// $ gradle drivers

ext {
    AUTO_IMPORTED_JAR_PREFIX = 'AI_'
    ExecutionProfilesLoaderVersion = "1.2.1"
    materialstoreVersion = '0.1.0-SNAPSHOT'
    subprocessjVersion = '0.1.0'
    ashotVersion = '1.5.4'
    jsoupVersion = '1.7.2'
}

repositories {
    mavenCentral()
    mavenLocal()
}

configurations {
    myconf
}

dependencies {
    myconf group: 'com.kazurayam', name: 'materialstore',
                    version: "${materialstoreVersion}"
    myconf group: 'com.kazurayam', name: 'ExecutionProfilesLoader',
                    version: "${ExecutionProfilesLoaderVersion}"
    myconf group: 'com.kazurayam', name: 'subprocessj',
                    version: "${subprocessjVersion}"
    myconf group: 'ru.yandex.qatools.ashot', name: 'ashot',
                    version: "${ashotVersion}"
    myconf group: 'org.jsoup', name: 'jsoup', version: "${jsoupVersion}"
}

task drivers {
    doFirst {
        delete fileTree("Drivers") {
            include("**/" + AUTO_IMPORTED_JAR_PREFIX + "*")
        }
    }
    doLast {
        copy { copySpec ->
            copySpec
                .from(project.getConfigurations().getByName("myconf"))
                .into("Drivers")
                .include(
                    "**/materialstore*.jar",
                    "**/ExecutionProfilesLoader*.jar",
                    "**/ashot*.jar",
                    "**/jsoup*.jar",
                    "**/subprocessj*.jar",
                    )
                .rename({ s ->
                    AUTO_IMPORTED_JAR_PREFIX + s
                    })
        }
    }
}