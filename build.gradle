// VisualInspectionInKatalonStudio_Reborn/build.gradle

// $ gradle drivers

version = '0.4.2'

ext {
    AUTO_IMPORTED_JAR_PREFIX = 'AUTOIMPORTED_'
    ExecutionProfilesLoaderVersion = "1.2.1"
    materialstoreVersion = '0.4.6-SNAPSHOT'
    ashotVersion = '1.5.4'
    javadiffutilsVersion = '4.9'
    jsoupVersion = '1.7.2'
    subprocessjVersion = '0.3.3'
    freemarkerVersion = '2.3.31'

    UITESTJAVA_PROJECT_DIR = '../../github/MyPythonProjectTemplate/uitestjava'
}

repositories {
    mavenCentral()
    mavenLocal()
}

configurations {
    myconf
}

dependencies {
    myconf group: 'com.kazurayam', name: 'materialstore',
                    version: "${materialstoreVersion}"
    myconf group: 'com.kazurayam', name: 'ExecutionProfilesLoader',
                    version: "${ExecutionProfilesLoaderVersion}"
    myconf group: 'com.kazurayam', name: 'subprocessj',
                    version: "${subprocessjVersion}"
    myconf group: 'ru.yandex.qatools.ashot', name: 'ashot',
                    version: "${ashotVersion}"
    myconf group: 'io.github.java-diff-utils', name: 'java-diff-utils',
                    version: "${javadiffutilsVersion}"
    myconf group: 'org.jsoup', name: 'jsoup', version: "${jsoupVersion}"

    myconf group: 'org.freemarker', name: 'freemarker', 
                    version: "${freemarkerVersion}"

}

task drivers {
    doFirst {
        delete fileTree("Drivers") {
            include("**/" + AUTO_IMPORTED_JAR_PREFIX + "*")
        }
    }
    doLast {
        copy { copySpec ->
            copySpec
                .from(project.getConfigurations().getByName("myconf"))
                .into("Drivers")
                .include(
                    "**/materialstore*.jar",
                    "**/ExecutionProfilesLoader*.jar",
                    "**/subprocessj*.jar",
                    "**/ashot*.jar",
                    "**/jsoup*.jar",
                    "**/java-diff-utils*.jar",
                    "**/freemarker*.jar"
                    )
                .rename({ s ->
                    AUTO_IMPORTED_JAR_PREFIX + s
                    })
        }
    }
}

/**
 * This task mports the Java source code from the [MyPythonProjectTemplate/uitestjava](https://github.com/kazurayam/MyPythonProjectTemplate/tree/develop/uitestjava) project.
 * The java code includes the "Page Object Model" to test the Falskr web app.
 */
task importSourceOfFlaskrPOM {
    doLast {
        delete files("Include/scripts/groovy/com/kazurayam/uitestjava")
        copy { copySpec ->
            copySpec
                .from("${UITESTJAVA_PROJECT_DIR}/src/main/java")
                .into("Include/scripts/groovy")
                .include(
                    "com/kazurayam/uitestjava/**/*"
                )
        }
    }
}
